defmodule BotTextClient.Bot do
  alias TextClient.{State, Summary, Mover}
  alias BotTextClient.Chooser

  # won, lost, good_guess, bad_guess, already_used, initializing
  def play(%State{tally: %{game_state: :won}}) do
    exit_with_message("You Won!")
  end

  def play(%State{tally: %{game_state: :lost}}) do
    exit_with_message("Sorry, you LOST!")
  end

  def play(game = %State{tally: %{game_state: :good_guess}}) do
    continue_with_message(game, "Good guess!")
  end

  def play(game = %State{tally: %{game_state: :bad_guess}}) do
    continue_with_message(game, "Sorry, bad guess!")
  end

  def play(game = %State{tally: %{game_state: :already_used}}) do
    continue_with_message(game, "Letter already used!")
  end

  def play(game) do
    continue(game)
  end

  def continue_with_message(game, message) do
    IO.puts(message)
    continue(game)
  end

  def continue(game) do
    game
    |> Summary.display()
    |> Chooser.choose_move()
    |> Mover.make_move()
    |> play()
  end

  defp exit_with_message(message) do
    IO.puts(message)
    exit(:normal)
  end
end